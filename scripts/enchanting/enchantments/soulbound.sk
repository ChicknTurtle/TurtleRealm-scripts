
on death of player:
    set {_e} to enchantment from key "turtle:soulbound"
    gamerule keepInventory of victim's world is false
    loop drops:
        loop-value is enchanted with {_e}
        remove loop-value from drops
        event.getItemsToKeep().add(loop-value)

on drop:
    player isn't sneaking
    set {_e} to enchantment from key "turtle:soulbound"
    event-item is enchanted with {_e}
    cancel event
    send actionbar "Â§cSneak to drop a soulbound item"
    play sound "minecraft:block.note_block.hat" with volume 0.25 to player

import:
    io.papermc.paper.event.block.BlockBreakBlockEvent

on mine:
    (tag values of block tag "shulker_boxes") contains event-block
    player's gamemode isn't creative
    set {_drops::*} to drops of event-block using player's tool
    set {_nbt} to empty nbt compound
    set compound tag "components" of {_nbt} to compound tag "components" of full nbt of event-block
    set {_e} to enchantment from key "turtle:soulbound"
    if dirt with nbt {_nbt} is enchanted with {_e}:
        loop {_drops::*}:
            set {_item} to loop-value
            enchant {_item} with {_e}
            set {_drops::%loop-iteration%} to {_item}
    if size of {_drops::*} > 0:
        clear drops
        drop {_drops::*} at location of event-block ~ vector(0.5,0.5,0.5)

on BlockBreakBlockEvent:
    (tag values of block tag "shulker_boxes") contains event.getBlock()
    set {_nbt} to empty nbt compound
    set compound tag "components" of {_nbt} to compound tag "components" of full nbt of event.getBlock()
    set {_e} to enchantment from key "turtle:soulbound"
    if dirt with nbt {_nbt} is enchanted with {_e}:
        set {_drops::*} to ...event.getDrops()
        event.getDrops().clear()
        loop {_drops::*}:
            set {_item} to loop-value
            enchant {_item} with {_e}
            set {_drops::%loop-iteration%} to {_item}
        drop {_drops::*} at location of event.getBlock() ~ vector(0.5,0.5,0.5)

# shulkers can't explode
on explode:
    loop exploded blocks:
        (tag values of block tag "shulker_boxes") contains loop-block
        remove loop-block from exploded blocks
on block explode:
    loop ...event.blockList():
        (tag values of block tag "shulker_boxes") contains loop-value
        event.blockList().remove(loop-value)
