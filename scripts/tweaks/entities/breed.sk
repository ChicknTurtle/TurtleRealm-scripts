
import:
    io.papermc.paper.event.block.BlockPreDispenseEvent
    org.bukkit.entity.Animals

on BlockPreDispenseEvent:
    set {_block} to event.getBlock()
    set {_block2} to block in front of {_block}
    set {_e::*} to entities within ({_block2} ~ vector(-0.5,-0.5,-0.5)) and ({_block2} ~ vector(0.5,0.5,0.5))
    set {_e::*} to {_e::*} where [input is instance of Animals]
    set {_e::*} to {_e::*} where [input.isBreedItem(event.getItemStack())]
    size of {_e::*} > 0
    # cancel if any valid mobs are there, even if they can't breed right now
    cancel event
    set {_e::*} to {_e::*} where [input can breed]
    set {_e::*} to {_e::*} where [love time of input is 0 seconds]
    set {_e} to first element of {_e::*}
    if {_e} is set:
        dispenserEvent({_block})
    else:
        dispenserEventFail({_block})
        exit trigger
    # set love mode, remove item
    remove 1 of event.getItemStack() from (slot event.getSlot() of inventory of {_block})
    set love time of {_e} to 30 seconds
    play entity effect love_hearts on {_e}
