
when ready to load recipes:
    register shaped recipe:
        id: "turtle:blazing_pickaxe"
        result: getItem("turtle:blazing_pickaxe")
        shape: "bgb", " s ", " s "
        category: "equipment"
        ingredients:
            set ingredient of "b" to blaze rod
            set ingredient of "g" to golden pickaxe
            set ingredient of "s" to netherite scrap

on break:
    # ignore if sneaking
    player isn't sneaking
    # doesn't change creative functionality
    gamemode of player isn't creative
    # check if player is using blazing pickaxe
    getId(player's tool) is "turtle:blazing_pickaxe"
    # doesn't work on containers
    inventory of event-block isn't set
    clear drops
    loop drops of event-block using player's tool as player:
        drop smelted loop-value at event-block's location ~ vector(0.5,0.5,0.5)
        if smelted loop-value isn't loop-value:
            draw 5 of flame at event-block with extra 0.05
