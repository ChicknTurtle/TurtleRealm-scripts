
import:
    io.papermc.paper.event.block.BlockPreDispenseEvent

on load:
    set {features::croptrample::name} to "Crop trampling"
    set {features::croptrample::desc} to "Crops cannot be trampled while wearing feather falling"
    set {features::croptrample::icon} to farmland
    set {features::bonemealing::name} to "Bonemealing"
    set {features::bonemealing::desc} to "Allows bonemealing blocks you normally can't\nCurrently only sugarcane"
    set {features::bonemealing::icon} to bonemeal
    set {features::planting::name} to "Planting"
    set {features::planting::desc} to "Allows planting certain items onto blocks\nCurrently just seeds on dirt to make grass"
    set {features::planting::icon} to wheat seeds

# no trample with feather falling
on block trample:
    {features::croptrample::enabled} is true
    if type of event-block is farmland:
        boots of event-entity are enchanted with feather falling
        cancel event

# bonemeal sugarcane
function bonemealSugarCane(block:block) :: boolean:
    {features::bonemealing::enabled} is true
    {_block} is sugar cane
    loop blocks between {_block} and location 1000 blocks above {_block}:
        if loop-block is sugar cane:
            continue loop
        if loop-block is air:
            set loop-block to sugar cane
            play sound "minecraft:item.bone_meal.use" at {_block}
            make 15 of happy_villager at {_block} with offset vector(0.25,0.25,0.25)
            return true
        return false

on right click on sugar cane:
    {features::bonemealing::enabled} is true
    player's tool is bonemeal
    if bonemealSugarCane(event-block) is true:
        make player swing their hand
        player's gamemode isn't creative
        remove 1 of bonemeal from player's tool

on BlockPreDispenseEvent:
    {features::bonemealing::enabled} is true
    event.getItemStack() is bonemeal
    if bonemealSugarCane(block in front of event.getBlock()) is true:
        cancel event
        remove 1 bonemeal from slot event.getSlot() of inventory of event.getBlock()
        play sound "minecraft:block.dispenser.dispense" at event.getBlock()

# apply seeds
on right click on dirt:
    {features::planting::enabled} is true
    player isn't sneaking
    player's tool is wheat seeds
    event-blockface is up_face
    cancel event
    set event-block to grass block
    play sound "minecraft:item.crop.plant" at event-block
    make player swing their hand
    player's gamemode isn't creative
    remove 1 of player's tool from player's tool
