
on damage of dropped item:
    boolean tag "custom;specialItem" of full nbt of victim is true
    cancel event

on item despawn:
    boolean tag "custom;specialItem" of full nbt of event-entity is true
    cancel event

on item merge:
    if any:
        boolean tag "custom;specialItem" of full nbt of event-entity is true
        boolean tag "custom;specialItem" of full nbt of future event-entity is true
    then:
        cancel event

on entity pickup:
    event-player isn't a player
    boolean tag "custom;specialItem" of full nbt of event-dropped item is true
    cancel event

on entity added to world:
    event-entity is a dropped item
    if boolean tag "custom;specialItem" of full nbt of event-entity is true:
        specialItemGlowLoop(event-entity)
    # for newly spawned items
    wait 1 tick
    if boolean tag "custom;specialItem" of full nbt of event-entity is true:
        specialItemGlowLoop(event-entity)

function specialItemGlowLoop(e:entity):
    while chunk at {_e} is loaded:
        make {_e} glow for all players
        wait 1 tick
