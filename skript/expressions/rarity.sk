
import:
    net.minecraft.core.component.DataComponents

# Item Rarity
# Get the rarity of an item
expression:
    patterns:
        [the] [item] rarity of %itemstack%
        %itemstack%'s [item] rarity
    get:
        return "%(1 of expr-1).handle.getComponents().get(DataComponents.RARITY)%"
    set:
        set string tag "minecraft:rarity" of nbt of raw expr-1 to change value

# Rarity Color
# Get the rarity color of an item
expression:
    patterns:
        [the] rarity color of %itemstack%
        %itemstack%'s rarity color
    get:
        set {_rarity} to rarity of expr-1
        if expr-1 is enchanted:
            if {_rarity} isn't "EPIC":
                return rgb(85,255,255)
        if {_rarity} is "UNCOMMON":
            return rgb(255,255,85)
        else if {_rarity} is "RARE":
            return rgb(85,255,255)
        else if {_rarity} is "EPIC":
            return rgb(255,85,255)
        else:
            return rgb(255,255,255)
